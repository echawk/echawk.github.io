<!DOCTYPE html>
<html>
<head>
<meta charset="utf-8" />
<meta name="viewport" content="width=device-width,initial-scale=1" />
<link rel="stylesheet" href="./style.css" />
<title></title>
</head>
<body>
<h1 id="deterministic-ssh-keys-via-common-lisp">deterministic ssh keys via common lisp</h1>
<p>Have you ever wanted to generate ssh keys <em>deterministically</em> in Common Lisp
by cheating the system?</p>
<p>If yes, read on!</p>
<p><strong>DISCLAIMER:</strong> I am not responsible when&#47;if someone cracks your ssh key. Please
don&#8217;t use this for anything serious&#47;public facing. If you get pwned, that&#8217;s your
fault.</p>
<p>Now, with that out of the way:</p>
<h2 id="the-beef">the <strong>beef</strong></h2>
<p>Here&#8217;s the code:</p>
<pre><code>;; SPDX-License-Identifier: BSD-2-Clause

(ql:quickload &#39;(:ironclad :cl-ssh-keys))

(defun generate-deterministic-keys (seed-string)
  "Will deterministically generate a ssh key pair from SEED-STRING.

The ssh key pair is returned as a list, with the private key being first
and the public key being second."
  (let* ((checksum-int
           (reduce #&#39;+ (mapcar #&#39;char-int (coerce seed-string &#39;list))))
         (sk (ironclad:ascii-string-to-byte-array seed-string))
         (pk (ironclad:ed25519-public-key sk))

         (ironclad-priv-key (ironclad:make-private-key :ed25519 :x sk :y pk))
         (ironclad-pub-key  (ironclad:make-public-key  :ed25519 :y pk))

         (key-type (ssh-keys:get-key-type-or-lose :ssh-ed25519 :by :id))

         (pub-key
           (make-instance &#39;ssh-keys:ed25519-public-key
                          :kind key-type
                          :y (ironclad:ed25519-key-y ironclad-pub-key)))
         (priv-key
           (make-instance &#39;ssh-keys:ed25519-private-key
                          :public-key pub-key
                          :cipher-name "none"
                          :kdf-name "none"
                          :checksum-int checksum-int
                          :kind key-type
                          :y (ironclad:ed25519-key-y ironclad-priv-key)
                          :x (ironclad:ed25519-key-x ironclad-priv-key))))
    (list priv-key pub-key)))

</code></pre>
<p>That&#8217;s it - I should mention that I had to look into the internals of how
cl-ssh-keys generates key pairs to get the above code, hence the license
identifier.</p>
<p>Otherwise the code is rather straightforward: turn our string into a byte array
that we can use with ironclad&#8217;s <code>make-private-key</code> &#38; <code>make-public-key</code>
functions. Then use those ed25519 keys that ironclad created as the base of our
ssh keys.</p>
<p>And for the obligatory example code, using the above function:</p>
<pre><code>(defun example ()
  (let* ((keyname  "example")
         (seed-str "your-super-secret-seed-string-please-protect-at-all-costs")
         (keys-lst (generate-deterministic-keys seed-str)))

    (ssh-keys:write-key-to-path
     (first keys-lst)
     (concatenate &#39;string (namestring (user-homedir-pathname)) ".ssh&#47;" keyname))

    (ssh-keys:write-key-to-path
     (second keys-lst)
     (concatenate &#39;string (namestring (user-homedir-pathname)) ".ssh&#47;" keyname ".pub"))))
</code></pre>
<p>While principally this is the only code that you need to get up and running, I
thought I&#8217;d give you an actually useful script. Since, well, your seed string
has to be rather long to be of any real practical use. It would be feasible
for an attacker to attempt to brute force your seed string if it was typed out
by a mere human.</p>
<p>Instead let&#8217;s be a tad more clever.</p>
<h2 id="the-accoutrement">the <em>accoutrement</em></h2>
<h3 id="using-a-password-manager-to-get-a-super-long-string">using a password manager to get a super long string</h3>
<p>If you have read articles on this blog before, you may be familiar with my
common lisp implementation of <a href="https://lesspass.com">lesspass</a>, which we can
contort to be useful for us at present.</p>
<p>Without getting too into the weeds about how lesspass works, it is essentially a
&#8220;password manager&#8221; that works by generating your password to a website on the
fly when you provide it the same information.</p>
<p>One of the other aspects that lesspass has baked in is the ability to change
your password entirely by changing the &#8220;count&#8221;, essentially just a number that
you can increase should your password be found in a data breach.</p>
<p>However, we can use this feature of lesspass to our advantage, by generating
multiple passwords from the same password profile, but by changing the count, we
can get and arbitrarily long &#8220;password&#8221;.</p>
<h4 id="some-code">some code&#8230;</h4>
<p>If we make a couple of assumptions about the password profile itself for
lesspass we can simplify a lot of the logic:</p>
<ul>
<li>Assume we are using all rules (lowercase, uppercase, digits, &#38; symbols)</li>
<li>Assume a length of 32</li>
</ul>
<p>And that&#8217;s about it. We can have the user provide the site and login,
say if they wanted to have different ssh keys for different git forges.</p>
<p>We&#8217;ll get back to the counter.</p>
<pre><code>(ql:quickload :lesspass)

;; example of our object.
(setq pass-prof
  (make-instance
   &#39;lesspass:password-profile
   :site ""
   :login ""
   :rules
   &#39;(lesspass:lowercase
    lesspass:uppercase
    lesspass:digits
    lesspass:symbols)
   :counter 1))

(setf (lesspass:site-of pass-prof) "website.com")

(setf (lesspass:login-of pass-prof) "your-name?")
</code></pre>
<p>Now before we can go about generating a big &#8220;password&#8221; we have to discuss
what we are going to do with the <code>counter</code>. Like we mentioned before we
can arbitrarily set counter to be anything to make a new password.</p>
<p>We can use the following function to get a reproducible &#8216;step-size&#8217; for a
string. It&#8217;s not exactly a perfect idea, but it seems to be obscured enough
away from the actual string that you&#8217;ll be feeding the function (hint:
your master password) that I feel alright using the number it generates.</p>
<pre><code>(defun get-step-size-from-string (str n)
  "Return a pseudo random number for a given STR.

Sums the value of all of the characters in STR and divides them by N."
  (let* ((entropy (reduce
                   #&#39;+
                   (mapcar
                    (lambda (ch)
                      (expt (char-int ch) 2))
                    (coerce str &#39;list))))
         (step (floor (coerce (&#47; entropy n) &#39;float) 1)))
    step))
</code></pre>
<p>Essentially we just sum up all of the char values in <code>str</code> and floor divide it
by <code>n</code>. Should give us a good integer to work with.</p>
<p>Now we can bring everything together in a function, aptly named
<code>get-seed-string</code>:</p>
<pre><code>(defun get-seed-string ()
  "Return a string that is &#39;good enough&#39; to seed ironclad with."
  (declare (optimize (safety 3)))
  (let* ((iters 50)
         (master-pass   "yourmasterpassword")
         (password-prof pass-prof)

         ;; Get the step value from the master password since that information
         ;; is also secret, thus making it more secure against brute forcing.
         (step (get-step-size-from-string
                master-pass iters)))
    (apply &#39;concatenate &#39;string
           (loop :for i :from 1 :to iters
                 ;; Ensure that the counter of the lesspass prof is some
                 ;; pseudo-random value. Each different value for the
                 ;; counter *will* change the password.
                 :do (setf (lesspass:counter-of password-prof)
                           (* i step))
                 :collect
                 (lesspass:generate-password password-prof master-pass)))))
</code></pre>
<p>Here we have a few more assumptions: have the number of iterations set to 50.
Combined with the passwords of length 32, we will always have a seed string
of length 1600. Seems secure <em>enough</em>.</p>
<p>The other important part here is multiplying the step value by i, and setting
that to be our counter before we generate our password. That will ensure
our generated passwords are unique.</p>
<h3 id="conclusion">conclusion</h3>
<p>That&#8217;s it. Just integrate that function into the previous code and you&#8217;re set.</p>
<h2 id="links">links</h2>
<p>If you want to checkout the version that I use and have in my dotfiles, the link
is <a href="https://github.com/echawk/dots/blob/master/scripts/.local/bin/det-ssh-keys.lisp">here</a></p>
<p>And here&#8217;s the links to the related libraries:</p>
<ul>
<li><a href="https://github.com/sharplispers/ironclad">ironclad</a></li>
<li><a href="https://github.com/dnaeon/cl-ssh-keys">cl-ssh-keys</a></li>
<li><a href="https://github.com/echawk/lesspass-cl">lesspass-cl</a></li>
</ul>
</body>
</html>
